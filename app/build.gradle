apply plugin: 'com.android.application'

// Needed for gRPC
apply plugin: 'com.google.protobuf'


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ln_zap.zap"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


//  Needed for gRPC
sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}


//  Needed for gRPC
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.17.1'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
                grpc {
                    // This will generate the ServiceGrpc
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // QR-Code scanner: To Do: check if this is secure
    implementation 'me.dm7.barcodescanner:zbar:1.9.8'

    // QR-Code generator: To Do: check if this is secure
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'

    // Library to restart app
    implementation 'com.jakewharton:process-phoenix:2.0.0'

    // HTTP library to perform API interaction
    implementation 'com.android.volley:volley:1.1.1'

    // Bitcoin implementation
    // implementation 'org.bitcoinj:bitcoinj-core:0.14.7'

    // Library to detect when app goes to background or foreground
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha01'

    // gRPC implementation to communicate with lnd
    implementation 'io.grpc:grpc-okhttp:1.17.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.17.1'
    implementation 'io.grpc:grpc-stub:1.17.1'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    compileOnly 'com.google.api.grpc:googleapis-common-protos:0.0.3'

    // Used for base encoding
    implementation 'com.google.guava:guava:27.0.1-android'

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha02'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
